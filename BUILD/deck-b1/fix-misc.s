Cs  The Misc Stuff Configs     03/03/2017 
Cs   
Cs  Configure BOOTSTRAP for MULTITYPE
Cs
Cs  *USESTA  - all link to 9/MONSYS
Cs  *STATBUF - disabling system logs/stats
Cs  EXCHNRS  - links FETCH to DR 22:37
Cs  WRITDISP - using the right MS image 
Cs
 *USESTA:,NAME,
C
C*****   OПPEДEЛEHИE MECTA ДHEBHOЙ CTATИCTИKИ
C
C.....   ИMЯ ЛEHTЫ / MOДEЛИ
 ,TEXT,8HUSESTA 0
 ,BSS,
C.....   HAЧ.HOMEP ЗOHЫ CTATИCTИKИ
 ,LOG,
C.....   HAЧ.TPAKT TAБЛИЦ 'PASS+ФИO'
  ,LOG,4 0000 0016 0002
C.....   MECTO TEPMИHAЛЬHOЙ CTATИCTИKИ
C        HA 9/MONSYS ДЛЯ ЭBM-2,ЭBM-1(0)
  ,LOG,0435 0436
 ,END,
 *STATBUF:,NAME,.
 ,LOG,0.PAЗP.CTATИC.
 ,LOG,1.HE HAДO CБPOCA
 ,TEXT,8HD73STA '6'
 ,OCT,0000 0010.ЗOHA HAЧAЛA
 ,LOG,107.L ДИH.БYФ. - 1
 ,LOG,0.YKAЗ.ДИH.БYФ.
 ,LOG,0.BЫЗBAH ДИCП.
 ,BSS,9.YCTPOЙCTBA
 ,BSS,110B.ДИH.БYФEP
 ,END,.
 EXCHNRS:,NAME,
C     PP 13-18 YKAЗATEЛЬ MБ
C     PP 1-5 HAЧAЛЬHЫЙ N TPAKTA
C     ДЛЯ HEPEЗИДEHTA MYЛЬTИTAЙПA
C        ,014,100B
C        ,025,37B
C     B CЛYЧAE ЗAГPYЗKИ HA ЛEHTY
C     39-Й PAЗPЯД = 0,  1-24 HE CYЩECTBEHHЫ
C
        ,010,100B
        ,067,140B+37B
 ,END,
 WRITDISP:,NAME,.04.05.76
C
C*****   ФOPMИPOBAHИE И ЗAПИCЬ ДИCПETЧEPA
C*****   И MOHИTOPHOЙ CИCTEMЫ
C*****   ( TT - COOБЩEHИE )
C
 MONITOR*:,SUBP,
  DATA*:,LC,2
C
C.....   CBЯЗИ C HOBЫM ДИCПETЧEPOM
C
 MAINSW:,SUBP,
  EMSW:,EQU,MAINSW+277B
 HAЧHИЗ:,EQU,64000B
 CДBHИЗ:,EQU,HAЧHИЗ-70000B
 KOHHИЗ:,LC,
 NP:,P/P,(KOHHИЗ)1024
 HAЧBEPX:,P*P,(NP+1)1024
 *DRUMBAS:LC,BLOCK,*B
C
C...   HAЧAЛO И KOHEЦ HACTPOЙKИ
C      (B OБCЛYГE HAЧAЛO = 70B)
C
  DISPBASE:LC,BLOCK,DD/KYC,DD/IW70
  *TLSIZE:,SUBP,
C
C
 EXCHNRS:,SUBP,
 KC0:,EQU,101B.ЭTAЛOH KC
 *677:B,BLOCK,BITS,B48,B47,(5),B41(2)
 ,CONT,B39,(7),B31,(12)
 ,CONT,B18,B17,B16,B15,(11)
 ,CONT,B3,B2,B1
 I:,EQU,3
 C3:,LC,1
 C377:,LC,1
 C77777:,LC,1
 STARTDD:,SUBP,
 CALCSUM:,SUBP,
  *D71DATA:,EQU,MAINSW+270B
 *TYP/MEM:,LC,1
 *PRT:LC,BLOCK,(4)
 *ИПД:LC,BLOCK,(5),XNC,XNCM
 NCS:,WEQ,XNC
 NCM:,WEQ,XNCM
  MTRACNT:,EQU,*ИПД+27B
 *ИПK1:LC,BLOCK,PRTSTAT
 TABPRT:LC,BLOCK,TPRT
 *0:,BLOCK,MINP,MAXP,NC,RP,RP1
  TPHYS:,LC,1
 *NRP1:,PC,
    FREEP:,P/P,(*NRP1)1024
   *NRP4:,LC,
  ENRP4:,LC,
  LMLT:,EQU,ENRP4-*NRP4
  BMLT:,P/P,(*NRP4)1024
  KMLT:,P/P,(LMLT )1024
C
C.....   PAБOЧИE ЯЧEЙKИ
C
 *10:B,BLOCK,INF0,INF,WS1,DELT,MTNRS,MMT
C
C.....   ПEPEПИCЬ 'WRITDISP' B БEЗOПACHOE MECTO
 START:1,VTM,*-END
 2,VTM,
 ПEPE:2,UTC,START
 ,XTA,
 2,UTC,74000B
 ,CTX,
 2,UTM,1
 1,VLM,ПEPE
 7,VTM,74000B
 7,REL,START
 ,UJ,CONT.HA HOBOE MECTO
C.....   ПEPEПИCЬ 'MAINSW' HA CBOE MECTO
 CONT:1,VTM,MAINSW-EMSW
 ,DRUM,SAVMNT
 2,VTM,
  13,VTM,*D71DATA
  ,XTA,DATA*
  13,ATX,
  ,XTA,DATA*+1
  13,ATX,1
 L1:2,UTC,MAINSW
 ,XTA,
 2,UTM,1
 2,CTX,477B
 1,VLM,L1
C       ДЛЯ ЗAПYCKA DD
 ,XTA,JSTDD
 ,CTX,1000B
 ,CTX,600B
 ,CTX,603B
C
C
C...   ИHBEPTИPYEM ДЛИHY OЧEPEДИ
C      ЗAKAЗOB HA OБMEH, C YЧETOM
C      HEOБXOДИMOГO PEЗEPBA ДЛЯ
C      OПOЗHABAHИЯ ДИCKOB.
C
    ,XTA,MTRACNT
   ,NTR,3
   ,A-X,=10
   ,AEX,=3777 7777 7777 7777
   ,ATX,MTRACNT
 I,VTM,PRTSTAT
 13,VJM,CORADDR
 1,VTM,NCM
 LP:1,UTM,-1
 ,XTA,B1-5
 1,UTC,
 I,ATX,NCS-NCM
 1,V1M,LP
 ,XTA,B1-4
 I,ATX,NCS-3
 ,XTA,B1-3
 I,ATX,NCS-4
 I,VTM,*PRT
 13,VJM,CORADDR
 ,XTA,FP
 ,ATX,MAXPS
 ,ARX,B3
 I,ATX,RP1
 I,ATX,RP
C.....   ФИKCAЦИЯ ЛИCTOB 1 PEЗИДEHTA
  I,VTM,TPHYS
  13,VJM,CORADDR
  1,VTM,FREEP
  ,XTA,B1
  FIXPAG:1,UTM,-1
  I,UTC,
  1,ATX,
  1,VIM,FIXPAG
  9,VTM,*B.*DRUMBAS
C.....   CДBИГ 2-OЙ ЧACTИ PEЗИДEHTA ДИCПETЧEPA
 1,VTM,1-4096
 2,VTM,HAЧHИЗ
 3,VTM,HAЧBEPX
 L2:2,XTA,
 3,CTX,
 2,UTM,1
 3,UTM,1
 1,VLM,L2
C.....   HACTPOЙKA ДИCПETЧEPA HA ЗAДAHHЫЙ MБ
  9,XTA,.*DRUMBAS
 ,ASN,64+12.ЗAПИCЬ
 ,ATX,MMT
 ,UZA,TO DR 1. HA MБ
 ,ASN,64+3
 ,UJ,TO DT 1. HA MЛ
  TO DR 1:9,XTA,.*DRUMBAS
 TO DT 1:,ASN,64-36
  ,ATX,SAVDB
 ,AUX,C770077
 ,AOX,B39
 ,ATX,INF
 ,ATX,INF0
 ,ATX,DD/IW70
 ,APX,C770037
 ,AUX,UPMKYC
 ,ATX,DD/KYC
C.....   PAЗMEЩEHИE MT HEPEЗИДEHTA
 ,UTC,EXCHNRS
 ,XTA,.MECTO
 ,ATX,MT NRS.MT-HEPEЗИД.
 ,AAX,B39
 ,U1A,FIXNRS.- ЗAДAHO
 ,XTA,DD/IW70
 ,ARX,D MTNRS
 ,UTC,EXCHNRS
 ,ATX,
C.....   HA ПOДCЧET KOHTP.CYMMЫ
  ,XTA,DMTNRS
  COPYNRPL:,AAX,C777
  ,AOX,SAVDB
  ,ATX,SAVDB
  ,XTA,
 ,CTX,102B
  ,CALL,CALCSUM
 ,ATX,KC0
C.....   ПOДГOTOBKA K ЗAПИCИ ДИCПETЧEPA
 ,XTA,
 ,ATX,DELT
 ,XTA,MMT
 ,UZA,WR DISP.ЗAПИCЬ HA MБ
  9,XTA,.*DRUMBAS
 ,ASN,64-33
 ,AUX,C770777
 ,ATX,INF
 ,AOX,B41
 ,ATX,MMT
C.....   OПPEДEЛEHИE ФИЗ.AДPECA ПO MЛ
C.....   ( ДЛЯ TT - ПEЧATИ )
 ,FUN,113B.
C   CYMMATOP = ДEЛЬTA, A ИP 14 = ФИЗ HOMEPY MФ.
 ,ATX,DELT
 ,ITA,14
 ,ASN,64-12
 ,ATX,INF0.ФИЗ. N MЛ
 ,XTA,INF
 ,AAX,C777.N HAЧ.ЗOHЫ БEЗ
 ,AOX,INF0.YЧETA ДEЛЬTЫ
 ,ATX,INF0.....
C.....   ЗAПИCЬ ДИCПETЧEPA HA MБ ИЛИ MЛ
 WR DISP:1,VTM,-NP-4
  ,XTA,INF
  ,ARX,=100 0000 0036
  ,ATX,TPП
  ,ARX,=1
  ,ATX,TPП+1
 L3:,*70,INF
 ,XTA,INF
 ,ARX,B31*1
 ,ATX,INF
 1,VLM,L3
 ,ATX,WS1
 ,ARX,C777.ИHДИKAЦ.
 ,ATX,INF.N KOH.ЗOHЫ
C
C.....  YCTAHOBKA HOMEPOB MAT.ЛИCTOB
C         ДЛЯ  MT - KAHAЛOB
  ,XTA,MT NRS
  ,AAX,=0004 3700 0077 7777
  ,ATX,W
  ,ASN,64+20
  ,ATI,1
  ,DRUM,MT NRS.ЧTEHИE NR-MT
  1,XTA,11B.CCЫЛKA
  ,AAX,=1777.B FETCH-E
  ,ATI,14
  14,J+M,1.BUFSAP
  14,VTM,KMLT
  ,XTA,=:0777 7774 172.3-25,32-35,37
  PSCAL:,ATX,LMEM
  ,XTA,=:4
  14,ASN,64+BMLT.ИCKЛЮЧEHИE
  ,AEX,LMEM.MT-ЛИCTOB
  14,VZM,EPSCAL
  14,UTM,-1
  ,UJ,PSCAL
  EPSCAL:2,VTM,-1
  3,VTM,-8
  PUT PAG:,ATX,LMEM
  ,ANX,=7777 7777
  ,ATI,14
  ,ASN,64-30
  3,UTC,8
  1,ATX,1
  ,XTA,=:4
  14,ASN,64
  ,AEX,LMEM
  3,VLM,PUT PAG
  3,VTM,-8
  1,UTM,10
  2,VLM,PUT PAG
  ,XTA,
  1,ATX,-20
  1,ATX,-10
  ,DRUM,W.ЗAПИCЬ NR-MT
 ,XTA,MT NRS
 ,AAX,B39.MECTO MT NRS
 ,U1A,L4.- ЗAДAHO
C.....   ПEPEПИCЬ MT HEPEЗИДEHTA
C.....   BCЛEД ЗA ДИCПETЧEPOM
 ,DRUM,MT NRS
 ,XTA,WS1
 ,AAX,MINF
 ,AOX,B31
 ,ATX,INF
 ,*70,INF
C
C.....    ЗAПИCЬ   T P П   HA  36-37
C.....    ЗOHЫ  ЭKCTEHTA  ДИCПETЧEPA
C
  ,XTA,INF
  ,AEX,TPП+1
  ,AAX,C777
  ,AEX,INF
  ,ATX,INF
  ,DRUM,I1/1
  ,TAPE,TPП
  ,DRUM,I1/2
  ,XTA,17B
  ,ATX,3774B
  ,XTA,
  ,ATX,3775B
  ,TAPE,TPП+1
  9,XTA,.CИCTEMY
 ,ASN,64+27.ПEPEПИCЫBATЬ
 ,UZA,L4. - HET
C.....   ПEPEПИCЬ MOHИTOPHOЙ CИCTEMЫ
 1,VTM,1-64
 ПEPEMC:,DRUM,INFMS
 ,XTA,INF
 ,ARX,B1
 ,ATX,INF
 ,TAPE,INF
 ,XTA,INFMS
 ,ARX,C7741
 ,AAX,Z12/6
 ,ATX,INFMS
 1,VLM,ПEPEMC
C.....   ПOДГOTOBKA И ПEЧATЬ TT-COOБЩEHИЯ
 L4:,XTA,INF0
 ,ASN,64-30
 ,AUX,UPM1
 ,AOX,CTT1.HOMEP
 ,ATX,TT+2.YCTPOЙCTBA
 1,VTM,-1
 L5:1,XTA,INF0+1
 ,ARX,DELT
 ,ASN,64-39
 ,AUX,UPM2.HOMEPA
 1,AOX,CTT2+1.HAЧ. И KOH.
 1,ATX,TT+4.  ЗOH
 1,VLM,L5
  14,VTM,NP+6
  ,ITA,14
  ,ASN,64-39
  ,AUX,=6H'''''7''7''7'''
  ,AOX,=6HД=000
  ,ATX,TT+5
 1,VTM,TT
 2,VJM,TT PR
 ,XTA,MMT.MБ-ЗAПИCЬ
 ,UZA,END WR. - ДA
 ,TAPE,MMT.ПOДBOД K HAЧAЛY
 END WR:,BSS,
  ,XTA,DISPBASE+1
  ,ASN,64+12
  ,ATI,14
  ,XTA,DISPBASE+4.ДИCПETЧEP
  14,AAX,B48-10B.HA ДИCKE
  ,UZA,NO SERV. - HET
C
C...  ЗAПИCЬ, ECЛИ HAДO, HACTPOЙKИ
C     B  0-Й ЛИCT OБCЛYГИ HA 9/MONSYS
C
  3,VTM,
  ,*70,=0014 0100 0016 0020
  1,VTM,DISPBASE-*TLSIZE
  2,VTM,2070B
  NEW GEN:1,XTA,*TLSIZE
  2,AEX,
  :,UZA,*+1
  3,UTM,1
  :2,AEX,
   2,ATX,
  2,UTM,1
  1,VLM,NEW GEN
  3,VZM,NO SERV.БEЗ ИЗM.
  1,VTM,-3
  ,XTA,=3053 4576 1634 0112
  ,*76,1
  NEW TRAN:1,XTA,*53
  1,CTX,2053B
  1,VLM,NEW TRAN
  ,*70,=0004 0100 0016 0020
  1,VTM,TNG
  2,VJM,TTPR
C---
  NO SERV:,BSS,
 ,DRUM,I1/1
 ,DRUM,I2/1
 ,DRUM,I1/2
 ,XTA,17B
 ,ATX,3774B.
 ,XTA,
 ,ATX,3775B
 ,DRUM,I2/2
 ,DRUM,RESMNT
  ,XTA,SAVDB
  ,ATX,120B
 ,XTA,=14 3700 0021 0000
 ,ATX,61B
 ,XTA,44B
 ,AOX,=:4
 ,ATX,44B
Cs
Cs It is not right, but not so crucial 
Cs ,UJ,MONITOR*+1
 ,UJ,MONITOR*+2
C       KOPPEKTИPOBKA AДP. ИЗ 2-Г0 PEЗИДEHTA
 CORADDR:,ITA,I
 ,AAX,B15
 13,UZA,
 I,UTM,CДBHИЗ
 13,UJ,
  JSTDD:6,VTM,STARTDD
  6,UJ,
  *50:6,VTM,DISPBASE-*TLSIZE
  2,VTM,70B
  *51:2,XTA,
  2,UTM,1
  *52:6,ATX,*TLSIZE+1024
  6,VLM,51B
  *53:,UTC,
 I1/1:,LOG,10 0100 0020 0000
Cs
Cs I2/1:,LOG,00 0100 0000 0034
Cs to DR 22.34
 I2/1:,LOG,00 0100 0067 0174
 I1/2:,LOG,10 0100 0020 0001
Cs
Cs I2/2:,LOG,00 0100 0000 0035
Cs to DR 22.35
 I2/2:,LOG,00 0100 0067 0175
 SAVMNT:,LOG,00 0000 0000 0033
 RESMNT:,LOG,10 0000 0000 0033
 HET MЛ:,BSS,
 1,VTM, T HET
 2,VTM,END WR
 TT PR:,BSS,
  ,ITA,1
  ,FUN,76B
  2,UJ,
  FIXNRS:,XTA,MTNRS
  ,UJ,COPYNRPL
  SAVDB:,LOG,
C.....
 TT:,ISO,12HOC '47'ДYБHA'47'
 ,BSS,4
  ,ISO,6HЗOH.''''
  TNG:,ISO,30HБЛOK BЫЗOBA ДИCПETЧEPA ЗAПИCAH
     :,ISO,30H B 420 ЗOHY ФAЙЛA 9/MONSYS.''
 T HET:,ISO,12HЗAKAЗ MЛ'47'''
 ,BSS,1
 UPM1:,GOST,6H000770
 CTT1:,ISO,6HHA 00/
 UPM2:,GOST,6H777000
 CTT2:,ISO,6H000-'1''1'
  ,CONT,6H000/.
 DMTNRS:,Z01,100B
 ,,NP+5
Cs
Cs  DD needs the real native MS!
Cs  so, geeting it from 9/MONSYS
Cs  INFMS:,010,100B
Cs  ,040,
 INFMS:,010,100B
 ,040,
 FP:,,
 ,,FREEP
 STEP:,LOG,1 00001
 MINF:,LOG,4 0000 0077 0777
 UPMKYC:,LOG,70 0000 0000 1774
 C770777:,LOG,77 0777
 C770077:,LOG,77 0077
 C770037:,LOG,77 0037
 C7741  :,LOG,7741
 C777   :,LOG,777
 Z12/6:,LOG,7777 7777 7777 0037
 B48/43:,OCT,77
 B31*1:,LOG,100 0000 0001
 MAXPS:,BSS,1
 W:,BSS,1
 LMEM:,BSS,1
  TPП:,BSS,2
 END:,BSS,1
 ,END,
